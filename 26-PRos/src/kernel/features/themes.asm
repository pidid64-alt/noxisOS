; -----------------------------
; Load and apply theme from THEME.CFG
; IN  : Nothing
; OUT : Nothing (carry flag set on error)
load_and_apply_theme:
    pusha

    call save_current_dir
    
    mov al, 'A'
    call fs_change_drive_letter

    call fs_parent_directory

    mov ax, conf_dir_name
    call fs_change_directory
    jc .error

    ; Load THEME.CFG file
    mov ax, theme_cfg_file
    mov cx, CFG_SCRATCH_OFF
    mov dx, CFG_SCRATCH_SEG
    call fs_load_huge_file
    jc .error

    ; Check if file is empty
    test ax, ax
    jne .nonempty
    test dx, dx
    je .error
.nonempty:

    ; Parse and apply theme
    push ds
    mov ax, CFG_SCRATCH_SEG
    mov ds, ax
    mov si, CFG_SCRATCH_OFF
    mov word [cs:.line_count], 0

.parse_loop:
    cmp word [cs:.line_count], 16
    jge .parse_ok

    ; Parse line: "index, r, g, b"
    call .parse_color_line
    jc .parse_failed

    inc word [cs:.line_count]
    jmp .parse_loop

.parse_failed:
    pop ds
    jmp .error

.parse_ok:
    pop ds
.done:
    clc
    popa

    call restore_current_dir

    ret

.error:
    stc
    popa
    call restore_current_dir
    ret

.parse_color_line:
    pusha

    call .skip_whitespace

    call .parse_number
    jc .parse_error
    mov [cs:.color_index], al

    call .skip_comma_and_space
    jc .parse_error

    call .parse_number
    jc .parse_error
    mov [cs:.red], al

    call .skip_comma_and_space
    jc .parse_error

    call .parse_number
    jc .parse_error
    mov [cs:.green], al

    call .skip_comma_and_space
    jc .parse_error

    call .parse_number
    jc .parse_error
    mov [cs:.blue], al

    call .skip_to_newline

    mov [cs:.next_si], si

    ; Translate color index to actual DAC register for VGA mode 0x12.
    ; In mode 0x12 the ATC remaps: colors 8-15 -> DAC 56-63, color 6 -> DAC 20.
    xor bx, bx
    mov bl, [cs:.color_index]
    mov bl, [cs:.atc_to_dac + bx]

    mov ax, 1010h
    mov bh, 0
    mov dh, [cs:.red]
    mov ch, [cs:.green]
    mov cl, [cs:.blue]
    int 10h

    popa
    mov si, [cs:.next_si]
    clc
    ret

.parse_error:
    popa
    stc
    ret

.skip_whitespace:
    push ax
.skip_ws_loop:
    lodsb
    cmp al, ' '
    je .skip_ws_loop
    cmp al, 9
    je .skip_ws_loop
    dec si
    pop ax
    ret

.skip_comma_and_space:
    push ax
    call .skip_whitespace
    lodsb
    cmp al, ','
    jne .skip_comma_error
    call .skip_whitespace
    pop ax
    clc
    ret
.skip_comma_error:
    pop ax
    stc
    ret

.skip_to_newline:
    push ax
.skip_nl_loop:
    lodsb
    cmp al, 0
    je .skip_nl_done
    cmp al, 10
    je .skip_nl_done
    cmp al, 13
    je .skip_nl_check_lf
    jmp .skip_nl_loop
.skip_nl_check_lf:
    lodsb
    cmp al, 10
    je .skip_nl_done
    dec si
.skip_nl_done:
    pop ax
    ret

.parse_number:
    push bx
    push cx

    xor ax, ax
    xor cx, cx

.parse_num_loop:
    push ax
    lodsb

    cmp al, '0'
    jb .parse_num_done_char
    cmp al, '9'
    ja .parse_num_done_char

    sub al, '0'
    mov bl, al
    pop ax

    mov bh, 10
    mul bh

    add al, bl
    inc cx
    jmp .parse_num_loop

.parse_num_done_char:
    pop bx
    dec si
    mov al, bl

    test cx, cx
    je .parse_num_error

    pop cx
    pop bx
    clc
    ret

.parse_num_error:
    pop cx
    pop bx
    stc
    ret

.line_count   dw 0
.color_index  db 0
.red          db 0
.green        db 0
.blue         db 0
.next_si      dw 0

; VGA mode 0x12 ATC -> DAC mapping for colors 0-15:
;   colors 0-5 direct, color 6 -> DAC 20, color 7 direct,
;   colors 8-15 -> DAC 56-63
.atc_to_dac   db 0, 1, 2, 3, 4, 5, 20, 7, 56, 57, 58, 59, 60, 61, 62, 63